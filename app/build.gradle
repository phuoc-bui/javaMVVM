apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    dataBinding.enabled = true

    // Create a variable called keystorePropertiesFile, and initialize it to your
    // keystore.properties file, in the rootProject folder.
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    if (keystorePropertiesFile.exists()) {
        Properties keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        android.buildTypes.release.signingConfig = signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Variable environment for productFlavors
    apply from: 'flavors.gradle'
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android support
    // Android support
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycleVersion"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$rootProject.lifecycleVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //database
    implementation 'androidx.room:room-runtime:2.1.0-alpha02'
    annotationProcessor 'androidx.room:room-compiler:2.1.0-alpha02'
    implementation 'androidx.room:room-rxjava2:2.1.0-alpha02'

    // Network
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"

    // Reactive
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjavaVersion"

    // Dagger
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"
    implementation "org.jetbrains:annotations:$rootProject.jetbrainsAnnotationsVersion"
    implementation "com.jakewharton.threetenabp:threetenabp:$rootProject.threetenabpVersion"

    // Image loader
    implementation "com.squareup.picasso:picasso:2.71828"

    // Custom layout
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'net.grandcentrix.tray:tray:0.12.0'
    implementation 'com.nikhilpanju.recyclerviewenhanced:recyclerviewenhanced:1.1.0'
    implementation 'com.daimajia.swipelayout:library:1.2.0@aar'
    implementation 'com.kevalpatel2106:ringtonepicker:1.1'
    implementation 'com.github.savvyapps:ToggleButtonLayout:1.2.0'
    implementation 'com.pacioianu.david:ink-page-indicator:1.3.0'

    //date picker
    implementation 'com.wdullaer:materialdatetimepicker:3.5.1'
    implementation 'com.karumi:dexter:4.2.0'
    implementation 'in.srain.cube:ultra-ptr:1.0.11'

    // testing
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.test:runner:$rootProject.runnerVersion"
    androidTestImplementation("androidx.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-v4:$rootProject.androidSupportVersion"
        force "com.android.support:support-v13:$rootProject.androidSupportVersion"
    }
}